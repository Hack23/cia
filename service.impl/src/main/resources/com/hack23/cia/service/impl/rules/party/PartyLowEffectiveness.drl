package com.hack23.cia.service.impl.rules.party

import org.kie.api.runtime.KieRuntime

import com.hack23.cia.model.internal.application.data.party.impl.ViewRiksdagenPartySummary
import com.hack23.cia.model.internal.application.data.committee.impl.ViewRiksdagenVoteDataBallotPartySummaryDaily
import com.hack23.cia.model.internal.application.data.committee.impl.ViewRiksdagenVoteDataBallotPartySummaryMonthly
import com.hack23.cia.model.internal.application.data.committee.impl.ViewRiksdagenVoteDataBallotPartySummaryAnnual
import com.hack23.cia.model.internal.application.data.rules.impl.Status
import com.hack23.cia.service.impl.rules.PartyComplianceCheckImpl
import com.hack23.cia.service.api.action.kpi.ComplianceCheck

rule "Party with low annual win rate - below 40%"
	dialect "java"
	salience 10
    when
        $p : PartyComplianceCheckImpl(party.activeParliament && party.party != "-" && annualSummary != null && annualSummary.partyWonPercentage < 40 && annualSummary.partyWonPercentage >= 30)
    then
    	$p.addViolation( Status.MINOR, "PartyLowEffectiveness","Behavior", kcontext.getRule().getName(),"LowWinRate");
end

rule "Party with very low annual win rate - below 30%"
	dialect "java"
	salience 50
    when
        $p : PartyComplianceCheckImpl(party.activeParliament && party.party != "-" && annualSummary != null && annualSummary.partyWonPercentage < 30 && annualSummary.partyWonPercentage >= 20)
    then
    	$p.addViolation( Status.MAJOR, "PartyLowEffectiveness","Behavior", kcontext.getRule().getName(),"VeryLowWinRate");
end

rule "Party with critically low annual win rate - below 20%"
	dialect "java"
	salience 100
    when
        $p : PartyComplianceCheckImpl(party.activeParliament && party.party != "-" && annualSummary != null && annualSummary.partyWonPercentage < 20)
    then
    	$p.addViolation( Status.CRITICAL, "PartyLowEffectiveness","Behavior", kcontext.getRule().getName(),"CriticallyLowWinRate");
end

rule "Party with low document productivity - below 5 docs per member last year"
	dialect "java"
	salience 10
    when
        $p : PartyComplianceCheckImpl(party.activeParliament && party.party != "-" && party.currentlyActiveMembers > 0 && party.avgDocumentsLastYear < 5 && party.avgDocumentsLastYear > 0)
    then
    	$p.addViolation( Status.MINOR, "PartyLowEffectiveness","Behavior", kcontext.getRule().getName(),"LowDocumentProductivity");
end
