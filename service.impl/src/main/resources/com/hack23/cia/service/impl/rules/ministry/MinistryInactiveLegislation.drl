package com.hack23.cia.service.impl.rules.ministry

import org.kie.api.runtime.KieRuntime

import com.hack23.cia.model.internal.application.data.ministry.impl.ViewRiksdagenMinistry
import com.hack23.cia.model.internal.application.data.rules.impl.Status
import com.hack23.cia.service.impl.rules.MinistryComplianceCheckImpl

rule "Ministry with few government bills last year"
	dialect "java"
	salience 10
    when
        // Threshold: Fewer than 2 government bills indicates low legislative initiative
        $p : MinistryComplianceCheckImpl(ministry.active && ministry.totalGovernmentBills < 2 && ministry.totalGovernmentBills > 0)
    then
    	$p.addViolation( Status.MINOR, "MinistryInactiveLegislation","Behavior", kcontext.getRule().getName(),"FewGovernmentBills");
end

rule "Ministry with no government bills"
	dialect "java"
	salience 50
    when
        $p : MinistryComplianceCheckImpl(ministry.active && ministry.totalGovernmentBills == 0)
    then
    	$p.addViolation( Status.MAJOR, "MinistryInactiveLegislation","Behavior", kcontext.getRule().getName(),"NoGovernmentBills");
end

rule "Ministry with no propositions"
	dialect "java"
	salience 100
    when
        $p : MinistryComplianceCheckImpl(ministry.active && ministry.totalPropositions == 0)
    then
    	$p.addViolation( Status.CRITICAL, "MinistryInactiveLegislation","Behavior", kcontext.getRule().getName(),"NoPropositions");
end
